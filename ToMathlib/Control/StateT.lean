/-
Copyright (c) 2024 Devon Tuma. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Devon Tuma
-/
module

public import PolyFun.Control.Monad.Free
public import ToMathlib.General
public import Batteries.Control.Lemmas

/-!
# Lemmas about `StateT`
-/

@[expose] public section

universe u v w

namespace StateT

variable {m : Type u → Type v} {m' : Type u → Type w}
  {σ α β : Type u}

instance [MonadLift m m'] : MonadLift (StateT σ m) (StateT σ m') where
  monadLift x := StateT.mk fun s => liftM ((x.run) s)

@[simp]
lemma liftM_of_liftM_eq [MonadLift m m'] (x : StateT σ m α) :
    (liftM x : StateT σ m' α) = StateT.mk fun s => liftM (x.run s) := rfl

lemma liftM_def [Monad m] (x : m α) : (liftM x : StateT σ m α) = StateT.lift x := rfl

@[simp]
lemma run_liftM [Monad m] (x : m α) (s : σ) :
    (liftM x : StateT σ m α).run s = x >>= fun a => pure (a, s) := rfl

-- TODO: should this be simp?
lemma monad_pure_def [Monad m] (x : α) :
    (pure x : StateT σ m α) = StateT.pure x := rfl

lemma monad_bind_def [Monad m] (x : StateT σ m α) (f : α → StateT σ m β) :
    x >>= f = StateT.bind x f := rfl

lemma monad_failure_eq [Monad m] [Alternative m] :
    (failure : StateT σ m α) = StateT.failure := rfl

@[simp]
lemma run_failure' [Monad m] [Alternative m] :
    (failure : StateT σ m α).run = fun _ => failure := by
  funext s
  simp

@[simp]
lemma mk_pure_eq_pure [Monad m] (x : α) :
  StateT.mk (fun s ↦ pure (x, s)) = (pure x : StateT σ m α) := rfl

/-! ## `StateT.run'` lemmas -/

section run'

variable [Monad m] [LawfulMonad m]

@[simp]
lemma run'_pure' (x : α) (s : σ) :
    (pure x : StateT σ m α).run' s = pure x := by
  simp [StateT.run'_eq]

@[simp]
lemma run'_bind' (x : StateT σ m α) (f : α → StateT σ m β) (s : σ) :
    (x >>= f).run' s = x.run s >>= fun ⟨a, s'⟩ => (f a).run' s' := by
  simp only [StateT.run'_eq, StateT.run, monad_bind_def, StateT.bind,
    map_eq_bind_pure_comp, bind_assoc]

@[simp]
lemma run'_map' (x : StateT σ m α) (f : α → β) (s : σ) :
    (f <$> x).run' s = f <$> x.run' s := by
  simp [StateT.run'_eq, Functor.map_map]

@[simp]
lemma run'_lift' (x : m α) (s : σ) :
    (StateT.lift x : StateT σ m α).run' s = x := by
  simp [StateT.run'_eq, map_eq_bind_pure_comp, bind_assoc]

/-- Running a `StateT` computation and projecting the value component equals `run'`:
`(x.run s >>= fun p => pure p.1) = x.run' s`. -/
@[simp]
theorem run_bind_fst_eq_run' (x : StateT σ m α) (s : σ) :
    (x.run s >>= fun p => pure p.1) = x.run' s := by
  simp [StateT.run'_eq, bind_pure_comp]

/-- Push `run'` through a `monadLift` bind: a lifted base computation `ma` binds before the state
is threaded, so `((liftM ma : StateT σ m α) >>= G).run' s = ma >>= fun a => (G a).run' s`. -/
theorem run'_monadLift_bind (ma : m α) (G : α → StateT σ m β) (s : σ) :
    ((liftM ma : StateT σ m α) >>= G).run' s = ma >>= fun a => (G a).run' s := by
  simp [StateT.run'_eq, StateT.run_bind, StateT.run_monadLift, bind_map_left, map_bind]

end run'

end StateT
